<?xml version="1.0" encoding="utf-8"?>
<detail:AttributeFormItem xmlns:fx="http://ns.adobe.com/mxml/2009" 
						  xmlns:s="library://ns.adobe.com/flex/spark" 
						  xmlns:mx="library://ns.adobe.com/flex/mx" 
						  xmlns:detail="org.openforis.collect.ui.component.detail.*" 
						  xmlns:input="org.openforis.collect.ui.component.input.*">
	
	<fx:Script>
		<![CDATA[
			import mx.containers.Grid;
			import mx.containers.GridItem;
			import mx.containers.GridRow;
			import mx.core.IVisualElement;
			import mx.core.IVisualElementContainer;
			
			import org.openforis.collect.i18n.Message;
			import org.openforis.collect.presenter.TaxonAttributeFormItemPresenter;
			
			import spark.components.Form;
			import spark.components.FormItem;
			import spark.components.Label;
			
			private static const INDENTED_FORM_ITEM_STYLE_NAME:String = "indent";
			
			override public function addTo(parent:IVisualElementContainer):void {
				if(this.isInDataGroup) {
					parent.addElement(codeTextInput);
					parent.addElement(scientificNameTextInput);
					parent.addElement(vernacularNameTextInput);
					parent.addElement(vernacularLangTextInput);
				} else {
					var formItem:spark.components.FormItem;
					addFormItem(parent, Message.get('edit.taxon.code'), codeTextInput);
					addFormItem(parent, Message.get('edit.taxon.scientificName'), scientificNameTextInput);
					addFormItem(parent, Message.get('edit.taxon.vernacularName'), vernacularNameTextInput);
					addFormItem(parent, Message.get('edit.taxon.vernacularLang'), vernacularLangTextInput);
				}
				initPresenter();
			}
			
			protected function addFormItem(parent:IVisualElementContainer, label:String, inputField:IVisualElement):void {
				if(parent is Form) {
					var formItem:spark.components.FormItem = new spark.components.FormItem();
					formItem.styleName = INDENTED_FORM_ITEM_STYLE_NAME;
					formItem.label = label;
					formItem.addElement(inputField);
					parent.addElement(formItem);
				} else if(parent is Grid) {
					var row:GridRow = new GridRow();
					var labelCol:GridItem = new GridItem();
					var l:Label = new Label();
					l.text = label;
					l.styleName = INDENTED_FORM_ITEM_STYLE_NAME;
					labelCol.addChild(l);
					row.addChild(labelCol);
					var attrCol:GridItem = new GridItem();
					attrCol.addChild(inputField as DisplayObject);
					row.addChild(attrCol);
					Grid(parent).addChild(row);
				}
			}
			
			override protected function initPresenter():void {
				presenter = new TaxonAttributeFormItemPresenter(this);
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<input:TextInput id="codeTextInput" width="100" />
		<input:TextInput id="scientificNameTextInput" width="100" />
		<input:TextInput id="vernacularNameTextInput" width="100" />
		<input:TextInput id="vernacularLangTextInput" width="100" />
	</fx:Declarations>
</detail:AttributeFormItem>
