<?xml version="1.0" encoding="utf-8"?>
<detail:AttributeFormItem xmlns:fx="http://ns.adobe.com/mxml/2009" 
						  xmlns:s="library://ns.adobe.com/flex/spark" 
						  xmlns:mx="library://ns.adobe.com/flex/mx" 
						  xmlns:detail="org.openforis.collect.ui.component.detail.*" 
						  xmlns:input="org.openforis.collect.ui.component.input.*"
						  >
	
	<fx:Script>
		<![CDATA[
			import mx.containers.Grid;
			import mx.containers.GridItem;
			import mx.containers.GridRow;
			import mx.core.IVisualElement;
			import mx.core.IVisualElementContainer;
			import mx.events.FlexEvent;
			
			import org.openforis.collect.Application;
			import org.openforis.collect.i18n.Message;
			import org.openforis.collect.metamodel.proxy.SpatialReferenceSystemProxy;
			import org.openforis.collect.presenter.CoordinateAttributeFormItemPresenter;
			
			import spark.components.Form;
			import spark.components.FormItem;
			import spark.components.Label;
			
			private static const INDENTED_FORM_ITEM_STYLE_NAME:String = "indent";
			
			override public function addTo(parent:IVisualElementContainer):void {
				if(this.isInDataGroup) {
					parent.addElement(srsDropDownList);
					parent.addElement(xTextInput);
					parent.addElement(yTextInput);
				} else {
					addFormItem(parent, Message.get('edit.coordinate.srs'), srsDropDownList);
					addFormItem(parent, Message.get('edit.coordinate.x'), xTextInput);
					addFormItem(parent, Message.get('edit.coordinate.y'), yTextInput);
				}
				initPresenter();
			}
			
			protected function addFormItem(parent:IVisualElementContainer, label:String, inputField:IVisualElement):void {
				if(parent is Form) {
					var formItem:spark.components.FormItem = new spark.components.FormItem();
					formItem.styleName = INDENTED_FORM_ITEM_STYLE_NAME;
					formItem.label = label;
					formItem.addElement(inputField);
					parent.addElement(formItem);
				} else if(parent is Grid) {
					var row:GridRow = new GridRow();
					var labelCol:GridItem = new GridItem();
					var l:Label = new Label();
					l.text = label;
					l.styleName = INDENTED_FORM_ITEM_STYLE_NAME;
					labelCol.addChild(l);
					row.addChild(labelCol);
					var attrCol:GridItem = new GridItem();
					attrCol.addChild(inputField as DisplayObject);
					row.addChild(attrCol);
					Grid(parent).addChild(row);
				}
			}
			
			override protected function initPresenter():void {
				presenter = new CoordinateAttributeFormItemPresenter(this);
			}

		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:DropDownList id="srsDropDownList" 
						width="150" 
						prompt="{Message.get('global.dropDownPrompt')}" 
						/>
		<input:TextInput id="xTextInput" />
		<input:TextInput id="yTextInput" />
	</fx:Declarations>
	
</detail:AttributeFormItem>
