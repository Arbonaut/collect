<?xml version="1.0" encoding="utf-8"?>
<input:InputField xmlns:fx="http://ns.adobe.com/mxml/2009" 
				  xmlns:s="library://ns.adobe.com/flex/spark" 
				  xmlns:mx="library://ns.adobe.com/flex/mx" 
				  xmlns:input="org.openforis.collect.ui.component.input.*"
				  currentState="descriptionVisible"
				  >
	<fx:Script>
		<![CDATA[
			import flash.sampler.getInvocationCount;
			
			import mx.collections.IList;
			
			import org.openforis.collect.metamodel.proxy.CodeAttributeDefinitionProxy;
			import org.openforis.collect.model.proxy.AttributeProxy;
			import org.openforis.collect.model.proxy.FieldProxy;
			import org.openforis.collect.presenter.CodeInputFieldPresenter;
			import org.openforis.collect.ui.Images;
			import org.openforis.collect.util.CollectionUtil;

			public static const STATE_DEFAULT:String = "default";
			public static const STATE_DESCRIPTION_VISIBLE:String = "descriptionVisible";
			
			private var _attributes:IList;
			private var _description:String;
			
			override protected function initPresenter():void {
				this.presenter = new CodeInputFieldPresenter(this);
			}
			
			override public function canApplyReasonBlank():Boolean {
				if(attributeDefinition != null && attributeDefinition.multiple) {
					if(CollectionUtil.isNotEmpty(attributes)) {
						if(attributes.length == 1) {
							var a:AttributeProxy = attributes.getItemAt(0) as AttributeProxy;
							var f:FieldProxy = a.getField(fieldIndex);
							return a.empty && f.symbol == null;
						} else {
							return false;
						}
					} else {
						return true;
					}
				} else {
					return super.canApplyReasonBlank();
				}
			}
			
			override public function hasReasonBlankSpecified():Boolean {
				if(attributeDefinition != null && attributeDefinition.multiple) {
					if(CollectionUtil.isNotEmpty(attributes) && attributes.length == 1) {
						var a:AttributeProxy = attributes.getItemAt(0) as AttributeProxy;
						var f:FieldProxy = a.getField(fieldIndex);
						return f.hasReasonBlankSpecified();
					} else {
						return false;
					}
				} else {
					return super.hasReasonBlankSpecified();
				}
			}

			protected function getFirstAttribute():AttributeProxy {
				if(CollectionUtil.isNotEmpty(attributes)) {
					var a:AttributeProxy = attributes.getItemAt(0) as AttributeProxy;
					return a;
				} else {
					return null;
				}
			}
			
			[Bindable]
			public function get attributes():IList {
				return _attributes;
			}
			
			public function set attributes(value:IList):void {
				_attributes = value;
			}
			
			[Bindable]
			public function get description():String {
				return _description;
			}
			
			public function set description(value:String):void {
				_description = value;
			}
			
			override public function set isInDataGroup(value:Boolean):void {
				super.isInDataGroup = value;
				if(value) {
					currentState = CodeInputField.STATE_DEFAULT;
				} else {
					currentState = CodeInputField.STATE_DESCRIPTION_VISIBLE;
				}
			}
			
		]]>
	</fx:Script>

	<input:states>
		<s:State name="default" />
		<s:State name="descriptionVisible" />
	</input:states>
	
	<s:HGroup width="100%" gap="2" verticalAlign="middle">
		
		<input:TextInput id="textInput" 
						 width="100%" 
						 height="100%"
						 toolTip.default="{description}"
						 />
		
		<s:Image id="openImage" 
				 source="{Images.OPEN_POPUP}" 
				 useHandCursor="true" 
				 buttonMode="true"
				 visible="{! CodeAttributeDefinitionProxy(attributeDefinition).external}"
				 includeInLayout="{! CodeAttributeDefinitionProxy(attributeDefinition).external}"
				 />
		
		<s:Label id="descriptionLabel" 
				 includeIn="descriptionVisible" 
				 text="{description}"
				 width="100"
				 maxDisplayedLines="1"
				 showTruncationTip="true"
				 />
		
	</s:HGroup>
	
</input:InputField>
